# echo "($(date +"%Y-%m-%d, %H:%M:%S")) Loading .zshrc"

# ─── Instant Prompt ─────────────────────────────────────────────────────────────
# Must stay at the top. Any code requiring console input goes ABOVE this block.
if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
  source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
fi

# ─── Oh My Zsh Configuration ────────────────────────────────────────────────────
export ZSH="$HOME/.oh-my-zsh"

# https://stackoverflow.com/questions/62931101/i-have-multiple-files-of-zcompdump-why-do-i-have-multiple-files-of-these
export ZSH_COMPDUMP="$ZSH/cache/.zcompdump-$HOST"

# Case-sensitive completion must be off. _ and - will be interchangeable.
HYPHEN_INSENSITIVE="true"

# zstyle ':omz:update' mode disabled  # disable automatic updates
# zstyle ':omz:update' mode auto      # update automatically without asking
zstyle ':omz:update' mode reminder    # just remind me to update when it's time
zstyle ':omz:update' frequency 13

COMPLETION_WAITING_DOTS="true"

# Uncomment to disable marking untracked files as dirty (faster for large repos)
# DISABLE_UNTRACKED_FILES_DIRTY="true"

# "mm/dd/yyyy"|"dd.mm.yyyy"|"yyyy-mm-dd" or strftime format
HIST_STAMPS="yyyy-mm-dd"

# Conditional plugin loading: minimal set for Claude Code
if [[ -z "$CLAUDECODE" ]]; then
  # Full plugin list for normal shell
  plugins=(
    fzf-tab gitfast ls cd-ls copypath copybuffer command-not-found
    autoupdate macos zsh-autosuggestions fast-syntax-highlighting asdf
    alias-tips git-it-on catimg isodate colored-man-pages hitchhiker
    dircycle auto-notify autoswitch_virtualenv iterm-tab-color
  )
  # temporarily removed: safe-paste
  # replaced: git with gitfast
else
  # Essential plugins for Claude Code
  plugins=(gitfast command-not-found asdf)
fi

ZSH_THEME=""  # using homebrew p10k, not OMZ theme

# ─── Environment (needed by PATH) ───────────────────────────────────────────────
export PNPM_HOME="$HOME/Library/pnpm"              # pnpm
export ANDROID_HOME="$HOME/Library/Android/sdk"   # android
export OLLAMA_ORIGINS="*"

# Perl local::lib
export PERL5LIB="$HOME/perl5/lib/perl5${PERL5LIB:+:$PERL5LIB}"
export PERL_LOCAL_LIB_ROOT="$HOME/perl5${PERL_LOCAL_LIB_ROOT:+:$PERL_LOCAL_LIB_ROOT}"
export PERL_MB_OPT="--install_base \"$HOME/perl5\""
export PERL_MM_OPT="INSTALL_BASE=$HOME/perl5"

# ─── PATH ───────────────────────────────────────────────────────────────────────
# Deduplicate PATH entries automatically
typeset -U PATH path

# Go environment (sets GOBIN, etc.)
[[ -f "$HOME/.asdf/plugins/golang/set-env.zsh" ]] && source "$HOME/.asdf/plugins/golang/set-env.zsh"

# Dotnet root for tooling (avoid "." / empty)
DOTNET_ROOT=""
if command -v asdf >/dev/null 2>&1; then
  dotnet_path="$(asdf which dotnet 2>/dev/null)" || dotnet_path=""
  [[ -n "$dotnet_path" ]] && DOTNET_ROOT="${dotnet_path:h}"   # :h = dirname in zsh
fi

# Only include optional paths if they're non-empty
path=(
  "/opt/homebrew/opt/coreutils/libexec/gnubin"                # gnubin
  "$HOME/.asdf/shims"                                         # asdf
  "$HOME/.dotnet/tools"                                       # dotnet tools
  ${GOBIN:+"$GOBIN"}                                          # go
  ${PNPM_HOME:+"$PNPM_HOME"}                                  # pnpm
  "$HOME/.cache/lm-studio/bin"                                # lm studio cli
  ${ANDROID_HOME:+"$ANDROID_HOME/platform-tools"}             # android
  "$HOME/perl5/bin"                                           # perl
  ${DOTNET_ROOT:+"$DOTNET_ROOT"}                              # dotnet
  "$HOME/bin"
  "$HOME/.local/bin"
  "/usr/local/bin"
  $path
)

# Regenerate asdf shims (set ASDF_RESHIM_ON_START=1 to enable, for A/B testing)
if command -v asdf >/dev/null 2>&1; then
  [[ -n "$ASDF_RESHIM_ON_START" ]] && asdf reshim
fi

# ─── Source Oh My Zsh ───────────────────────────────────────────────────────────
# compinit is inside this script
source "$ZSH/oh-my-zsh.sh"

# ─── Environment (post-OMZ) ─────────────────────────────────────────────────────
# Preferred editor for local and remote sessions
if [[ -n $SSH_CONNECTION ]]; then
  export EDITOR='nano'
else
  export EDITOR='code --wait'
fi

# history size fix
export HISTSIZE=99999
export SAVEHIST=$HISTSIZE

# ─── Tool Initialization ────────────────────────────────────────────────────────
# Powerlevel10k theme
source /opt/homebrew/share/powerlevel10k/powerlevel10k.zsh-theme

# To customize prompt, run `p10k configure` or edit ~/.p10k.zsh.
[[ -f "$HOME/.p10k.zsh" ]] && source "$HOME/.p10k.zsh"

# https://github.com/Homebrew/homebrew-command-not-found
if command -v brew >/dev/null 2>&1; then
  HB_CNF_HANDLER="$(brew --repository)/Library/Taps/homebrew/homebrew-command-not-found/handler.sh"
  [[ -f "$HB_CNF_HANDLER" ]] && source "$HB_CNF_HANDLER"
fi

# https://github.com/junegunn/fzf?tab=readme-ov-file#setting-up-shell-integration
command -v fzf >/dev/null 2>&1 && source <(fzf --zsh)

# keep at bottom - https://github.com/romkatv/powerlevel10k/issues/1554
unset ZSH_AUTOSUGGEST_USE_ASYNC

# ─── Functions ──────────────────────────────────────────────────────────────────
# list preferred Wi-Fi networks
function get_wifi_networks() {
  local wifi_networks
  wifi_networks=$(networksetup -listpreferredwirelessnetworks en0) || {
    echo "Failed to retrieve Wi-Fi network list."
    return 1
  }
  echo "$wifi_networks" | sed -e 's/^ *//'
}

# send macos notification if prompted for assword
function notify_on_password_prompt() {
  local cmd="${1//\"/\\\"}"
  if [[ "$cmd" =~ "assword" ]]; then
    osascript -e "display notification \"cmd: $cmd\" with title \"password needed\""
  fi
}

# benchmark zshrc load time
# usage: zshrc_bench [-v] [runs] [warmup] [file]
# examples:
#   zshrc_bench              # 10 runs, 1 warmup
#   zshrc_bench 30           # 30 runs
#   zshrc_bench 30 3         # 30 runs, 3 warmup
#   zshrc_bench -v 20 2      # verbose per-run output
#   zshrc_bench 20 2 ~/.zshrc.new   # explicit file
# A/B test asdf reshim:
#   zshrc_bench 20 2
#   ASDF_RESHIM_ON_START=1 zshrc_bench 20 2
function zshrc_bench() {
  emulate -L zsh
  setopt localoptions no_aliases pipefail

  local verbose=0
  if [[ "$1" == "-v" ]]; then
    verbose=1
    shift
  fi

  local runs=${1:-10}
  local warmup=${2:-1}
  local file=${3:-$HOME/.zshrc}

  if (( runs < 1 || warmup < 0 || warmup >= runs )); then
    echo "usage: zshrc_bench [-v] [runs>=1] [warmup>=0<runs] [file]"
    return 2
  fi
  [[ -f "$file" ]] || { echo "file not found: $file"; return 2; }

  local -a times
  local i out ms rc

  local cmd='
    zmodload zsh/datetime
    t0=$EPOCHREALTIME
    source "$ZSHRC_BENCH_FILE" >/dev/null 2>&1
    rc=$?
    t1=$EPOCHREALTIME
    printf "%.0f %d\n" $(( (t1 - t0) * 1000.0 )) $rc
  '

  # warmup (ignored)
  for (( i = 1; i <= warmup; i++ )); do
    ZSHRC_BENCH_FILE="$file" zsh -i -f -c "$cmd" >/dev/null
  done

  # measured runs
  for (( i = 1; i <= runs; i++ )); do
    out=$(ZSHRC_BENCH_FILE="$file" zsh -i -f -c "$cmd")
    ms=${out%% *}
    rc=${out##* }
    times+=("$ms")

    if (( verbose )); then
      printf "run %2d: %4d ms  rc=%d\n" $i $ms $rc
    fi

    if (( rc != 0 )); then
      printf "warning: source returned rc=%d on run %d\n" $rc $i >&2
    fi
  done

  # stats
  local -a sorted
  sorted=("${(@on)times}")
  local sum=0
  for ms in "${times[@]}"; do (( sum += ms )); done

  local n=${#times}
  local min=${sorted[1]}
  local max=${sorted[-1]}
  local avg=$(( sum / n ))

  local median
  if (( n % 2 == 1 )); then
    median=${sorted[$(( (n+1)/2 ))]}
  else
    median=$(( (sorted[n/2] + sorted[n/2+1]) / 2 ))
  fi

  local p95_idx=$(( (95*n + 99) / 100 ))
  local p95=${sorted[$p95_idx]}

  echo "zshrc_bench: $file  runs=$n  warmup=$warmup  (ms)"
  echo "min=$min  median=$median  avg=$avg  p95=$p95  max=$max"
}

# ─── Aliases: Navigation ────────────────────────────────────────────────────────
alias dl="cd $HOME/Downloads"
alias dt="cd $HOME/Desktop"
alias dv="cd $HOME/dev"
alias cbc="cd $HOME/dev/cypher-backend-core"
alias cad="cd $HOME/dev/cypher-admin-dashboard"
alias mow="cd $HOME/dev/merge-of-wonders"

# ─── Aliases: Claude ────────────────────────────────────────────────────────────
alias claude="$HOME/.claude/local/claude"
alias claudep='cd $HOME/dev/personal && CLAUDE_CONFIG_DIR=$HOME/.claude-personal claude'
alias claudepany='CLAUDE_CONFIG_DIR=$HOME/.claude-personal claude'

alias cbcc="cd $HOME/dev/cypher-backend-core && claude"
alias cadc="cd $HOME/dev/cypher-admin-dashboard && claude"
alias mowc="cd $HOME/dev/merge-of-wonders && claude"
alias cbccad="cd $HOME/dev/cypher-backend-core && claude --add-dir $HOME/dev/cypher-admin-dashboard"
alias cadcbc="cd $HOME/dev/cypher-admin-dashboard && claude --add-dir $HOME/dev/cypher-backend-core"
alias mowcbc="cd $HOME/dev/merge-of-wonders && claude --add-dir $HOME/dev/cypher-backend-core"
alias cbcmow="cd $HOME/dev/cypher-backend-core && claude --add-dir $HOME/dev/merge-of-wonders"
alias cbccadmow="cd $HOME/dev/cypher-backend-core && claude --add-dir $HOME/dev/cypher-admin-dashboard --add-dir $HOME/dev/merge-of-wonders"

# ─── Aliases: Git ───────────────────────────────────────────────────────────────
# fancy git log
alias gitlog="git log --graph --format=format:'%C(bold blue)%h%C(reset) - %C(bold green)(%ar)%C(reset) %C(white)%an%C(reset)%C(bold yellow)%d%C(reset) %C(dim white)- %s%C(reset)' --all"

# https://github.com/peterhurford/git-it-on.zsh
alias gitito="gitit branch master"

# ─── Aliases: Utilities ─────────────────────────────────────────────────────────
alias history="history 1"           # history size fix
alias python='python3'
alias bu='brew update && brew upgrade'
alias zshconfig="subl $HOME/.zshrc"
alias wifi-list='get_wifi_networks'

# show all files in a tree structure (works with ls zsh plugin)
alias lta="lt -a"

# show all files whose names begin with '.' except . and ..
alias lsa="ls -A"

# ─── Hooks ──────────────────────────────────────────────────────────────────────
autoload -Uz add-zsh-hook
add-zsh-hook preexec notify_on_password_prompt
